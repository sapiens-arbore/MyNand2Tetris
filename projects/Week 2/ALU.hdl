// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //ZeroX
    Mux16(a[0..15]=x, b[0..15]=false, sel=zx, out=ZeroXfunc);
    //NotX
    Not16(in[0..15]=ZeroXfunc, out=NotX);
    Mux16(a=ZeroXfunc, b=NotX, sel=nx, out=NotXfunc);
    //ZeroY
    Mux16(a[0..15]=y, b[0..15]=false, sel=zy, out=ZeroYfunc);
    //NotY
    Not16(in[0..15]=ZeroYfunc, out=NotY);
    Mux16(a=ZeroYfunc, b=NotY, sel=ny, out=NotYfunc);
    //ADD
    Add16(a[0..15]=NotXfunc, b[0..15]=NotYfunc, out=AddXY);
    //AND
    And16(a[0..15]=NotXfunc, b[0..15]=NotYfunc, out=AndXY);
    //Select which one to use
    Mux16(a[0..15]=AndXY, b[0..15]=AddXY, sel=f, out=AndOrAddXY);
    //Negate the output
    Not16(in=AndOrAddXY, out=NotAndOrAddXY);
    //Select negate or not
    Mux16(a[0..15]=AndOrAddXY, b[0..15]=NotAndOrAddXY, sel=no, out[0..15]=out, out[0..15]=output, out[15]=outfora);
    //Zr
    Nor16Way(in[0..15]=output, out=zr);    
    //Ng
    And(a=outfora, b = true, out=ng);


}
